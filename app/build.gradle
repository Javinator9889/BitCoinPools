apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.google.firebase.firebase-perf'

android {
    signingConfigs {
        release {
            configSign()
        }
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "javinator9889.bitcoinpools"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 105
        versionName "1.19.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    bundle {
        language {
            enableSplit = false
        }
        density {
            enableSplit = true
        }
        abi {
            enableSplit = true
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "app_version",
                    "${defaultConfig.versionName}"
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            pseudoLocalesEnabled false
            zipAlignEnabled true
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            debuggable true
            resValue "string", "app_version",
                    "${defaultConfig.versionName}${versionNameSuffix}"
        }
        dev {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "app_version",
                    "${defaultConfig.versionName}"
            debuggable true
            jniDebuggable true
            renderscriptDebuggable true
            pseudoLocalesEnabled false
            zipAlignEnabled true
        }
    }
    dexOptions {
        preDexLibraries = false
    }
    buildToolsVersion '28.0.3'
}

def configSign() {
    if (project.hasProperty("sign.properties") && new File(project.property("sign.properties"))
            .exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(file(project.property("sign.properties"))))
        keyAlias props['alias']
        keyPassword props['key.password']
        storeFile file(props['keystore'])
        storePassword props['keystore.password']
    }
}

def build_param = "${build}"

if (build_param != "dev") {
    //exclude production build
    android.variantFilter { variant ->
        if (variant.buildType.name.equals('dev')) {
            variant.setIgnore(true)
        }
    }
} else {
    //exclude all except production build
    android.variantFilter { variant ->
        if (!variant.buildType.name.equals('dev')) {
            variant.setIgnore(true)
        }
    }
}

tasks.withType(JavaCompile) {
    options.fork = true
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.vectordrawable:vectordrawable-animated:1.0.0'
    implementation 'androidx.media:media:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.constraintlayout:constraintlayout-solver:1.1.3'
    implementation 'com.google.firebase:firebase-core:16.0.5'
    implementation 'com.google.firebase:firebase-crash:16.2.1'
    implementation 'com.google.firebase:firebase-invites:16.0.5'
    implementation 'com.google.firebase:firebase-appindexing:17.1.0'
    implementation 'com.google.firebase:firebase-perf:16.2.1'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.6@aar') {
        transitive = true
    }
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0-alpha'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation 'androidx.preference:preference:1.0.0'
    implementation 'com.google.android.material:material:1.1.0-alpha01'
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation "com.mikepenz:aboutlibraries:6.2.0"
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.annotation:annotation:1.0.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'org.sufficientlysecure:donations:2.6'
    implementation group: 'com.googlecode.charts4j', name: 'charts4j', version: '1.3'
}
apply plugin: 'com.google.gms.google-services'
